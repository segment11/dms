plugins {
    id 'groovy'
}

group 'org.segment'
version '1.2'

final String artifactId = 'dms_agent'

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public'
    }
    maven {
        url 'https://s01.oss.sonatype.org/content/groups/public'
    }
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

sourceCompatibility = 17
targetCompatibility = 17

compileGroovy {
    groovyOptions.configurationScript = file("config.groovy")
}

configurations {
    runtime.exclude group: "org.slf4j", module: "slf4j-log4j12"
    compile.exclude group: "org.slf4j", module: "slf4j-log4j12"

    runtime.exclude group: "com.h2database", module: "*"
    compile.exclude group: "com.h2database", module: "*"

    runtime.exclude group: "mysql", module: "*"
    compile.exclude group: "mysql", module: "*"

    runtime.exclude group: "com.alibaba", module: "druid"
    compile.exclude group: "com.alibaba", module: "druid"
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.20.0'

    implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.12'

    implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.83'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.21'
    implementation group: 'commons-net', name: 'commons-net', version: '3.6'

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    implementation group: 'com.google.auto.value', name: 'auto-value', version: '1.7.2'

    implementation group: 'com.github.docker-java', name: 'docker-java', version: '3.2.13'
    implementation group: 'com.github.kevinsawicki', name: 'http-request', version: '6.0'

    implementation group: 'io.prometheus', name: 'simpleclient', version: '0.15.0'
    implementation group: 'io.prometheus', name: 'simpleclient_httpserver', version: '0.15.0'
    implementation group: 'io.prometheus', name: 'simpleclient_hotspot', version: '0.15.0'

    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.49.v20220914'

    implementation group: 'redis.clients', name: 'jedis', version: '4.3.1'

    implementation group: 'com.jcraft', name: 'jsch', version: '0.1.55'

    implementation group: 'io.github.segment11', name: 'segmentd', version: '1.2-SNAPSHOT'
    implementation group: 'io.github.segment11', name: 'segmentweb', version: '1.2-SNAPSHOT'
    implementation group: 'io.github.segment11', name: 'segment_common', version: '1.2-SNAPSHOT'

    implementation fileTree(dir: 'vendor', includes: ['*.jar', '*.so', '*.dll'])

    implementation project(':dms_common')
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes "Main-Class": 'RunAgent'
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() || it.name.endsWith('.so') || it.name.endsWith('.dll') ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "conf.properties"
        exclude "*.so"
        exclude "*.dll"
    }
    zip64 true
}

task copySo(type: Copy) {
    from configurations.runtimeClasspath.findAll {
        it.name.endsWith('.so') ||
                it.name.endsWith('.dll')
    }
    into 'build/libs'
}

task tar(type: Tar) {
    from jar
    from configurations.runtimeClasspath.findAll {
        it.name.endsWith('.so') ||
                it.name.endsWith('.dll')
    }
    destinationDirectory = file('.')
    archiveFileName = 'agentV2.tar.gz'
    compression = Compression.GZIP
}

tar.dependsOn jar, copySo

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
