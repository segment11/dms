plugins {
    id 'groovy'
    id 'java'
}

group 'org.segment'
version '1.0'

final String artifactId = 'dms_agent'

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public'
    }
//    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

compileGroovy {
    groovyOptions.configurationScript = file("config.groovy")
}

configurations {
    runtime.exclude group: "org.slf4j", module: "slf4j-log4j12"
    compile.exclude group: "org.slf4j", module: "slf4j-log4j12"

    runtime.exclude group: "com.h2database", module: "*"
    compile.exclude group: "com.h2database", module: "*"

    runtime.exclude group: "mysql", module: "*"
    compile.exclude group: "mysql", module: "*"

    runtime.exclude group: "com.alibaba", module: "druid"
    compile.exclude group: "com.alibaba", module: "druid"
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.17.1'

    compile group: 'org.codehaus.groovy', name: 'groovy', version: '2.4.19'

    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.83'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.18'
    compile group: 'commons-net', name: 'commons-net', version: '3.6'

    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    compile group: 'com.google.auto.value', name: 'auto-value', version: '1.7.2'

    compile group: 'com.github.docker-java', name: 'docker-java', version: '3.2.13'
    compile group: 'com.github.kevinsawicki', name: 'http-request', version: '6.0'

    compile group: 'io.prometheus', name: 'simpleclient', version: '0.9.0'
    compile group: 'io.prometheus', name: 'simpleclient_httpserver', version: '0.9.0'

    compile fileTree(dir: 'vendor', includes: ['*.jar', '*.so', '*.dll'])

    compile project(':dms_common')
    compile project(':segmentweb')
    compile project(':segmentd')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes "Main-Class": 'RunAgent'
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() || it.name.endsWith('.so') || it.name.endsWith('.dll') ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "conf.properties"
        exclude "*.so"
        exclude "*.dll"
    }
    zip64 true
}

task copySo(type: Copy) {
    from configurations.runtimeClasspath.findAll {
        it.name.endsWith('.so') ||
                it.name.endsWith('.dll')
    }
    into "$buildDir/libs"
}

task buildToRun(type: Copy) {
    from 'src/conf.properties'
    into "$buildDir/libs"
}

buildToRun.dependsOn copySo, jar